eval 'exec perl -w -S $0 ${1+"$@"}' # -*- Mode: perl -*-
    if 0;
# the above is a more portable way to find perl
# ! /usr/bin/perl
#                              -*- Mode: CPerl -*- 
# psh.PL --- simple perl shell mechanisms using AUTOLOAD to evaluate
#            undefined functions as system commands
# Author          : Marcel Turcotte
# Created On      : Wed Apr  8 10:33:17 1998
# Last Modified By: Goetz Pfeiffer
# Last Modified On: Wed Jul  4          2007
# Modifications   : changes in psh_run() for supporting
#                   multi-line commands
#                   a multi-line starts with "<" on a single input line
#                   and ends with ">" on a single input line
#                   in case of an error, all lines are printed 
#                   with line-numbers prepended
# Last Modified By: Biomolecular Modelling Admin
# Last Modified On: Fri Apr  7 14:04:29 2000
# Last Modified By: Bob MacCallum
# Last Modified On: Tue Apr 21 17:21:27 1998
# Modifications   : parenthesis balancing
# Last Modified On: Wed Apr 22 13:50:36 1998
# Modifications   : reading $ENV{HOME}/.pshrc, backgrounding, <<EOF
# Last Modified On: Wed Apr 22 21:32:28 1998
# Modifications   : basic readline thingy (bad for long lines) and up/down
# Last Modified On: Thu Apr 23 11:41:54 1998
# Modifications   : some fixes and simplifications
# Last Modified On: Fri Apr 24 11:38:50 1998
# Modifications   : system substitution for things like /bin/ls
# Modifications   : help command in bob's .pshrc
# Last Modified On: Wed Apr 29 10:21:00 1998
# Modifications   : changed ESC-s to do search and replace
# Modifications   : added $autosys thing
# Last Modified On: Tue Aug 11 11:48:28 1998
# Modifications   : removed & backgrounding and added bg function in .pshrc
# ==========================================
# Last Modified On: Tue Sep 15 17:42:25 1998
# Modifications   : MAJOR rewrite using Term::Readline::Gnu
#                   removed autosys (never used it, not very perl-like)
#                   history now done by readline
# Last Modified On: Wed Sep 23 13:03:29 1998
# Modifications   : fixed completion to complete variables up to common
#                   stem from completions
# Last Modified On: Wed Oct  7 13:52:53 BST 1998
# Modifications   : now runs ok on systems without the Gnu Readline library
#                 : (you still need Term::Readline of course)
#
# Last Modified On: Wed Jun  9 15:59:18 1999
# Modifications   : Major enhancement of completion function to include
#                   * Package::Name::completion 
#                   * $object->method_name completion
#                   * simple !prefix<tab> history completion
#                   * Package name after 'use'

#_ psh (c) 1998 Marcel Turcotte and Bob MacCallum 
#_ modified by Goetz Pfeiffer, 2007
#_
#_ Description
#_ ^^^^^^^^^^^
#_ Simple perl shell, uses AUTOLOAD to make system calls for commands which
#_ are not defined.  Multiple line entry is accomplished by adding a
#_ tailing \.  or by leaving an unbalanced (, { or [ (or <<EOF).
#_ Force execution (the balancing check isn't very clever) by terminating
#_ a line with ;; (two semicolons)
#_
#_ If you need a unix command instead of its perl namesake (eg. grep) then 
#_ prepend an underscore, eg: _grep('pattern', 'file') or you could use
#_ system() or backticks 
#_ 
#_ .pshrc   ($ENV{HOME}/.pshrc)
#_ ^^^^^^
#_ You can put your own functions and other startup code in your .pshrc
#_
#_ To use unix `ls' (for example) without having to write ls() put prototypes
#_ in your .pshrc like this
#_   sub ls;
#_   sub emacs;
#_
#_ Aliases are done like this:
#_   sub ll { ls('-l', @_) };
#_
#_ changing the behaviour in .pshrc
#_ ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
#_ the $prompt string is eval'ed to make the prompt: for example
#_ $prompt = '"psh<".($readline_attribs->{history_base}
#_                    +$readline_attribs->{history_length}).">% "';
#_
#_ set this variable like this:
#_ $psh_keywords_h = '/usr/local/src/perl5.004_01/keywords.h';
#_ to get all the perl keywords completed (grep foreach etc)
#_
#_ and $psh_history_size = 50
#_ to change the size of the history between invocations (default 100)
#_
#_ Miscellaneous
#_ ^^^^^^^^^^^^^
#_ If you need to `source' a perl program use perl's `do' function
#_ or if it's a module, use `use' of course.
#_
#_ line editing/file/command/variable completion by the Gnu ReadLine library
#_
#_ plus... Control-g : kill-whole-line
#_
#_ add your own changes using .inputrc or in your .pshrc using 
#_   $readline_terminal->parse_and_bind("Control-g: kill-whole-line");
#_   (see the GNU Readline user manual)
#_
#_ some `built-in' variables that you can use
#_
#_ $lr is the last result
#_ to echo $lr after each command set
#_ $echolr = 1 
#_
#_ Multi-Line mode
#_ ^^^^^^^^^^^^^
#_ Multi-line mode starts with '<' on a single line. All lines that are 
#_ then entered are collected until a '>' on a single line is encountered.
#_ By this you can test small functions by copying them with a simple
#_ cut & paste operation from your favorite editor to psh. The Multi-line
#_ mode is ended with '>'. If the commands contain errors, the line number
#_ with the error is printed and all lines entered so far are printed 
#_ with prepended line-numbers helping you finding the error in your
#_ definition. 
#_
#_ Job control (backgrounding)
#_ ^^^^^^^^^^^
#_ Control-z will background a running job (not suspend it like csh)
#_ ***this does not work for unix/system commands, use bg (see below) ***
#_ the `listjobs' command will list process ids (pid) of running jobs 
#_ to kill a job use: kill 9, pid  (the perl function)
#_
#_ to background perl code or other commands from the outset use:
#_ bg { code }
#_


use Term::ReadLine;
use Term::ReadLine::Gnu;

if (defined $ARGV[0] && $ARGV[0] eq '-h')
  { prhelp();
    exit(0);
  }

psh_init();
psh_run();

sub AUTOLOAD 
{
  my $program = $AUTOLOAD;
  $program =~ s/.*:://;
  $program =~ s/^_//;    # leading underscore
  $SIG{TSTP} = 'IGNORE'; # ctrl-z doesn't work for system calls
  return system($program, @_);
}

my $multi_line_mode;
my $last_multi_line;

sub linedump
  { return if (!defined $last_multi_line);
    my @lines= split(/\n/,$last_multi_line);
    my $i=1;
    foreach my $l (@lines)
      { printf("%3d %s\n",$i++,$l); }
  }

sub h_center
# center a text by adding spaces on both sides
  { my($st)= @_;
    return( (' ' x (38 - length($st)/2)) . $st );
  }

sub psh_run
{
  my $psh_balanced = 0;

  $| = 1;
  
  print h_center("*** perl shell ***"),"\n\n";
  print "type \"help\" for a short help on commands\n";
  
  
  $prompt = '"psh> "' if (not defined $prompt);

  $lr = undef;
  $psh_current_line = '';

  $SIG{INT} = 'IGNORE';
  $SIG{TSTP} = 'IGNORE';
  if ($autologout) {
    $SIG{ALRM} = sub { die "*** autologout ***\n"; };
    alarm $autologout;
  }
  while (defined ($_ = $readline_terminal->readline(eval($prompt).'  'x$psh_balanced))) 
  {
    alarm 0 if ($autologout);
    $psh_current_line .= "$_\n";
    ($psh_current_line, $psh_balanced) = psh_eval_line($psh_current_line);

    if ($autologout) {
      $SIG{ALRM} = sub { die "*** autologout ***\n"; };
      alarm $autologout;
    }
  }
}

sub psh_init
{
  $SIG{__WARN__} = sub {};
  $readline_terminal = new Term::ReadLine 'psh';
  $SIG{__WARN__} = 'DEFAULT';

  $readline_features = $readline_terminal->Features;

  if (exists $readline_features->{attribs})
  {
    $readline_attribs = $readline_terminal->Attribs;
    
    $readline_attribs->{attempted_completion_function} = \&psh_complete;
    $readline_attribs->{special_prefixes} = '$@%';
    $readline_attribs->{completer_word_break_characters} =~ s/\{//g;

    # added for $object->method() completion
    $readline_attribs->{completer_word_break_characters} =~ s/[<>]//g;

    $readline_attribs->{completion_append_character} = '';

    $readline_terminal->parse_and_bind("Control-g: kill-whole-line");
  }

  if (exists $readline_features->{readHistory} &&
      exists $readline_features->{stiflehistory})
  {
    $psh_history_size = 100 unless ($psh_history_size);
    $readline_terminal->stifle_history($psh_history_size);
    if (-e "$ENV{HOME}/.psh-history") {
      $readline_terminal->ReadHistory("$ENV{HOME}/.psh-history");
    }
  }

  if (-e "$ENV{HOME}/.pshrc") {
    do "$ENV{HOME}/.pshrc";
  } elsif ($ENV{PSHRC} && -e $ENV{PSHRC}) {
    do $ENV{PSHRC};
  }

  if (defined $psh_keywords_h && open(TOKE, $psh_keywords_h))
  {
    while (<TOKE>) {
	push @keywords, $1 if (/^#define KEY_(\w+)/);
    }
    close(TOKE);
  }
}

sub END
{
  if (exists $readline_features->{writeHistory})
  { 
    $readline_terminal->WriteHistory("$ENV{HOME}/.psh-history");
  }
}

sub psh_balanced
{
    # check for balanced brackets etc
    my ($l) = @_;
    # remove all escaped and quoted brackets
    $l =~ s/\\[(){}\[\]]//g;
    $l =~ s/".*?"|'.*?'//g;
    # remove all (.+) [.+] etc from inside out...
    while ($l =~ s/ \([^()]*\) | \[[^\[\]]*\] | \{[^{}]*\} //sx) { }
    # how many are left?		
    my @unmatched = $l =~ /([({[])/g;

    # look for heretext (only one level at present)
    my ($h) = @_;			 
    if (@unmatched == 0 && $h =~ /<<\s*(\S.*)$/m) {  
      my $start = quotemeta($1);
      my $end = $1;
      $end =~ s/^["'`]//;  $end =~ s/["'`;\s]+$//;
      $end = quotemeta($end);
      return $h !~ /$start.+$end\n/s; 
    }
    return scalar(@unmatched);
}

sub psh_eval_line
{
    ($_) = @_;

#    # remove comments (VERY SIMPLE)
#    s/#[^;]*$//;         # remove lines that are only comments

    if (s/\s*>\s*$//)
      { $multi_line_mode= 0;
	print "multi line mode OFF\n";
	$last_multi_line= $_; 
      }
      
    if (m/^\s*<\s*$/)
      { $multi_line_mode= 1; 
	print "multi-line mode on, leave it with '>' on a single line\n"; 
	$_=""
      }
      
    if ($multi_line_mode)
      { #s/\n+$//;
	return ($_, 0); 
      }

    # enable this for debugging:
    #print "L:|$_|\n";
   
    # does it end with a slash?
    return ($_, 0) if (s/\\\s*$//);

    # if not ending with a ;; is it balanced?
    my $bal = psh_balanced($_);
    return ($_, $bal) if ($bal > 0 && !/;;\s*$/);

    $SIG{INT} = sub { die "killed\n"; };
    $SIG{TSTP} = \&psh_background unless (/system|sleep/);
                                         # ctrl-z doesn't work for system calls
    # now eval it!!
    $lr = eval;

    $SIG{INT} = 'IGNORE';
    $SIG{TSTP} = 'IGNORE';

    if ($@)
      { linedump();
        print $@; # print possible error
      };  
    print "$lr\n" if (defined $lr && defined $echolr && $echolr);

    $last_multi_line= undef;
    return ('', 0);
}

sub psh_desc_pkg_tree {
  my $package = shift;
  my @result = ();
  foreach $subpackage (eval '@'.$package.'ISA') {
    push @result, psh_desc_pkg_tree($subpackage.'::');
  }
  return @result, keys %$package;
}

sub psh_complete
{
  my ($start, $text) = @_;
  my $qmstart = quotemeta($start);
  my @completions = ();

  # just an 'ls' not all the perl stuff
  return () if ($start eq '');

  # inferior history expansion because it can't do '!$xyz'
  # or '!sub abc' (with whitespace) as readline gets there first on these
  # occasions, but then again maybe you wouldn't want it to...
  if ($start =~ /^!(.+)/) {
    my ($result, $expansion) = $readline_terminal->history_expand($start);
    return ($expansion) if ($result);
  }


  # simple hash key completion
  if ($start =~ /^\$((?:\w+(?:::)?)+)\{(.*)$/)
  { 
    my ($hashname, $keystart) = ($1,quotemeta($2));
    my @keys = grep /^$keystart/, keys %$hashname;
    $keys[0] =~ s/^(\d+\D.*|.*\W.*)$/'$1'/ if (@keys == 1);
    push @completions, map "\$$hashname\{$_\}", @keys; 
  }
      
  my $package = 'main::';
  my $ppfx = '';


  my @objectkeys = ();

  # and $objects-> 
  if ($start =~ /^ \$ (\w+) /x) {
#print STDERR "++";
    my $grab = $1;  
    my $refresult = ref $$grab;
    if ($refresult && $refresult !~ /^(REF|SCALAR|ARRAY|HASH|CODE|GLOB)$/) {
      @objectkeys = eval "keys %{\$$grab}";
      $package = $refresult;
      $package .= '::';
      ($ppfx = $start) =~ s/->?[\w{}]*$//;
      $ppfx .= '->';
#      print STDERR "==";
    }
#print STDERR "--";
  }

  # sort out Packages:: if not found an object
  if (!$ppfx && $start =~ /((\w+::)+)/) {
#print STDERR "**";
    $package = $1;
    $ppfx = $package;
  }

  push @completions, grep /^$qmstart/ && !/^\$\_\</, 
                     map psh_namefix($_), 
                     map "$ppfx$_", psh_desc_pkg_tree($package);

  push @completions, grep /^$qmstart/, map "$ppfx\{$_\}", @objectkeys;
  push @completions, grep /^$qmstart/, @keywords;

  push @completions, glob "$qmstart*" if (@completions);

  # package names from @INC
  if ($text =~ /\buse\s+$qmstart$/) { 
    # reset completions
    @completions = grep /^$qmstart/, 
                   map { /(\w+).pm/; $1 } map glob("$_/*.pm"), (@INC, '.');
  }


  my %got;
  @completions = grep { !$got{$_} && ($got{$_}=1) } @completions;

  if (@completions==1)
  {
    # if it's PackageX::
    if ($completions[0] =~ /(\W*?)((\w+::)+)$/) {
      my ($symbol, $package) = (quotemeta($1), $2);
      $symbol = $package unless ($symbol);
      return ($completions[0], 
	      grep /^$symbol/,
	      map psh_namefix($_), 
	      map "$package$_", 
	      keys %$package);
    }

    # if it's an $object 
    if ($completions[0] =~ /^\$ (\w+) $/x) {
#print STDERR "---hello---\n";
      my $objname = $1;
      my $refresult = ref $$objname;
      if ($refresult && $refresult !~ /^(REF|SCALAR|ARRAY|HASH|CODE|GLOB)$/) {
	# we've got the package of the object
	$refresult .= '::';
	my @objectkeys = eval "keys %{$completions[0]}";
	return ($completions[0], 
		$completions[0], 
		(map "$completions[0]\-\>$_", psh_desc_pkg_tree($refresult)),
	        (map "$completions[0]\-\>\{$_\}", @objectkeys));
      }
    }

    # if the sole completion starts with a $variable containing a valid directory...
    if ($completions[0] =~ /^\$\w+$/)
    {
      my $evalled = eval "$completions[0]";
      return ("$evalled/") if ($evalled && 
			       $evalled !~ /\s/ &&
			       -d $evalled);
    }

    # if the sole completion is a hash index return the keys
    if ($completions[0] =~ /\$((?:\w+(?:::)?)+)\{$/)
    {
      my $hashname = $1;
      return @completions, map "\$$hashname\{$_\}", keys %{$hashname};
    }
  }



  # complete common stem of completions or ''
  if (@completions>1)
  {
    my $stemlength = 0;
    my %stems;
    my $common = '';
    while (keys(%stems)<=1)
    {
      $stemlength++; %stems = ();
      grep { $stems{substr($_, 0, $stemlength)}=1 } @completions;
    }
    $common = substr($completions[0], 0, $stemlength-1);
    unshift @completions, $common;
  }

  return @completions;
}

sub psh_namefix
{
  my ($key) = @_;
  my @result = ();
  return ($key) if ($key =~ /->\w+$/); # have to do this otherwise
                                       # all the names end up in the main:: scope
  push @result, "\$$key" if (defined $$key);
  push @result, "\$$key\[" if (defined @$key);
  push @result, "\$$key\{" if (defined %$key && $key !~ /::$/);
  push @result, "\@$key" if (defined @$key);
  push @result, "\%$key" if (defined %$key);
  push @result, $key if (defined &$key);
  if ($key =~ /::$/ && $key !~ /->/) {
    push @result, $key, "\$$key", "\@$key", "\%$key";
  }
  return @result;
}

sub psh_background # ctrl-z backgrounding
{
  my $pid;

  if ($pid = fork())
  {
    #parent 
    push @psh_jobs, $pid;
    die "backgrounded $pid\n";
  }  
  else
  {
    #child
  }
}

sub bg (&)  # backgrounding on purpose (like 'emacs &' in csh)
{ 
  my ($code) = @_;
  my $pid; 
  if ($pid=fork())
  {
    push @psh_jobs, $pid;
    print "backgrounded $pid\n";
  }
  else
  {
    $SIG{INT} = 'IGNORE';
    &$code; exit 
  }
}

sub listjobs
{
  my $lastnum = 0;
  while (@psh_jobs != $lastnum)
  {
    $SIG{CHLD} = sub { wait };
    $lastnum = @psh_jobs;
    @psh_jobs = grep { kill 0, $_ } @psh_jobs;
  }
  print "backgrounded jobs: @psh_jobs\n" if (@psh_jobs);
  $SIG{CHLD} = 'DEFAULT';
}

sub prhelp
{ 
  open(ME, $0) || die;
  while (<ME>) { print $_ if (s/^#_ ?//); }
  close(ME);
}

sub help 
{ 
  open(ME, $0) || die;
  my $pager = 'more';
  $pager = $ENV{PAGER} if (exists $ENV{PAGER});
  open(PAGER, "|$pager") || die "psh help can't pipe to pager $pager\n";;
  print PAGER "*** press `q' to quit help `h' for help with pager ***\n\n";
  while (<ME>) { print PAGER $_ if (s/^#_ ?//); }
  close(ME);
  close(PAGER);
}
