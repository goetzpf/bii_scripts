#!/usr/bin/zsh -f
# -*- shell-script -*-

# Copyright 2015 Helmholtz-Zentrum Berlin f√ºr Materialien und Energie GmbH
# <https://www.helmholtz-berlin.de>
#
# Author: Thomas Birke <Thomas.Birke@helmholtz-berlin.de>
#
# This program is free software: you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free Software
# Foundation, either version 3 of the License, or (at your option) any later
# version.
# 
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along with
# this program.  If not, see <http://www.gnu.org/licenses/>.

usage () {
    <<EOF
usage: pvgrep [-io] [-flat] [-ext <ext>] <regexp>

grep for lines matching <regexp> in files produced by IOCs

Options:
  -flat       suppress IOC name, just print output unindented
  -ext <ext>  search files with extension <ext> instead
              default is dbl, -io sets ext to dbhcr
              possible extensions are: 
               activated, dbhcr, dbior, dbl, pwd, version and versions
  -i          search case-insensitive
  -io         search dbhcr-files (short for -ext dbhcr)
  -ioc <GLOB> search only IOCS matching GLOB pattern (default: *)
EOF
    return
}
[[ $# -eq 0 ]] && usage

zparseopts -D i=icase io=oio flat=oflat ext:=oext ioc:=iocglob

flat=0
indent="  "
ext=dbl
[[ $#icase -gt 0 ]] && greparg=-i
[[ $#oio -gt 0 ]] && ext=dbhcr
[[ $#oflat -gt 0 ]] && flat=1 indent=""
[[ $#oext -gt 1 ]] && ext=$oext[2]
[[ $#iocglob -gt 1 ]] && iocglob=$iocglob[2] || iocglob="*"

setopt extendedglob
setopt nullglob

cd /opt/IOC/log/Database

ioclist=($(eval print '(#i)**/'$iocglob.$ext '**/'${iocglob}_$ext.txt))

/bin/egrep $greparg --color=auto "$1" /dev/null $ioclist 2>/dev/null | grep -v 0x00000000 | perl -e "
while (<>) { 
    (\$ioc,\$rest,\$var)=split qq([._]$ext([^:]*):); 
    \$iocs->{\$ioc}->{\$var}=1;
}; 
for \$ioc (sort(keys(%{\$iocs}))) {
    print \"\$ioc:\\n\" unless $flat; 
    for \$var (sort(keys(%{\$iocs->{\$ioc}}))) { print(\"$indent\$var\"); }
}"
